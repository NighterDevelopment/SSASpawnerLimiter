plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}
group = 'github.io'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "enginehub"
        url = "https://maven.enginehub.org/repo/"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")
    implementation 'com.github.NighterDevelopment:PluginUpdateCore:1.0.0'
    implementation 'com.github.NighterDevelopment:PluginLangCore:1.0.0'
    compileOnly 'com.github.NighterDevelopment:SmartSpawner:1.5.7'

    // SQLite for database storage
    implementation 'org.xerial:sqlite-jdbc:3.47.1.0'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

jar {
    archiveBaseName.set("SSAddon-SpawnerLimiter")
    archiveVersion.set("${version}")
    from sourceSets.main.output
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    // Include dependencies in the jar
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    // Exclude META-INF signatures
    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
}
