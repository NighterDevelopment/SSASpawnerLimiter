plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.2.2'
}
group = 'github.io'
version = '1.0.0'

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://oss.sonatype.org/content/groups/public/" }
    maven { url = "https://maven.enginehub.org/repo/" }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    // Compile-time dependencies
    annotationProcessor("org.projectlombok:lombok:1.18.42")
    compileOnly("org.projectlombok:lombok:1.18.42")
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")
    compileOnly("com.github.NighterDevelopment:SmartSpawner:1.5.7.1")

    // Shadowed dependencies
    implementation("com.github.NighterDevelopment:PluginUpdateCore:1.0.1")
    implementation("com.github.NighterDevelopment:PluginLangCore:1.0.2")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml', 'paper-plugin.yml']) {
        expand props
    }
}


jar {
    archiveBaseName.set("SSAddon-SpawnerLimiter")
    archiveVersion.set("${version}")
    from sourceSets.main.output
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    enabled = false // Disable default jar task
}

shadowJar {
    archiveBaseName.set("SSAddon-SpawnerLimiter")
    archiveVersion.set("${version}")
    archiveClassifier.set('')

    // Relocate dependencies to avoid conflicts
    relocate 'io.github.pluginupdatecore', 'github.io.ssaspawnerlimiter.libs.pluginupdatecore'
    relocate 'io.github.pluginlangcore', 'github.io.ssaspawnerlimiter.libs.pluginlangcore'

    // Reduce jar size by merging service files and minimizing
    mergeServiceFiles()
    minimize()

    exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA'
    // destinationDirectory = file('C:\\Users\\notni\\OneDrive\\Desktop\\paper_1.21.8\\plugins')
}

build {
    dependsOn shadowJar
}